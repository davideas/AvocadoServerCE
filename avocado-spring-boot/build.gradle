apply plugin: 'java'
apply plugin: 'flyway'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'war'
apply plugin: 'idea' // https://docs.gradle.org/3.3/userguide/idea_plugin.html

group = 'eu.davidea'
version = '0.0.1-SNAPSHOT'
description = 'An opinionated setup for a modern RESTful Server'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    appName = 'Avocado Server Community Edition'
    builtBy = System.properties['user.name'] + "@" + getTimestamp()
}

// load config for active profile
def propertyDrivenProfile = configForActiveProfile()

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

configurations {
    providedRuntime
    compile.exclude module: 'spring-boot-starter-tomcat' // We use external Tomcat
    compile.exclude group: 'org.apache.tomcat' // We use external Tomcat
    compile.exclude group: 'org.yaml' // Not used
    compile.exclude group: 'org.objenesis' // Not used
    compile.exclude group: 'org.ow2.asm' // Not used
    compile.exclude group: 'ch.qos.logback' // We use Log4j2
    compile.exclude group: 'junit' // We use JUnit 5 Jupiter
}

/*
 * Gradle dependencies tree
 * Run > gradle module:dependencies
 *       --configuration runtime | compile | testCompile | providedCompile  | providedRuntime
 *       --dependency artifactId
 */
dependencies {
    /*
     * Server libraries
     */
    compile("javax.validation:validation-api:${javaxValidationVersion}")
    compile("org.springframework.boot:spring-boot-starter-web")
    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    compile("mysql:mysql-connector-java:${mysqlConnectorVersion}")
    compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:${myBatisBootVersion}")
    compile("org.flywaydb:flyway-core")
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-log4j2
    //compile("org.springframework.boot:spring-boot-starter-log4j2:${springBootVersion}")
    compile("org.slf4j:slf4j-api:${slf4jVersion}")
    runtime("org.slf4j:jcl-over-slf4j:${slf4jVersion}")
    runtime("org.slf4j:log4j-over-slf4j:${slf4jVersion}")
    runtime("org.apache.logging.log4j:log4j-api:${log4j2Version}")
    runtime("org.apache.logging.log4j:log4j-core:${log4j2Version}")
    runtime("org.apache.logging.log4j:log4j-jcl:${log4j2Version}")
    runtime("org.apache.logging.log4j:log4j-web:${log4j2Version}")
    runtime("org.apache.logging.log4j:log4j-slf4j-impl:${log4j2Version}")
    // https://mvnrepository.com/artifact/com.lmax/disruptor
    compile("com.lmax:disruptor:${log4j2DisruptorVersion}")
    // http://www.mindrot.org/projects/jBCrypt
    compile("de.svenkubiak:jBCrypt:${jBCryptVersion}")

    compileOnly("javax.servlet:javax.servlet-api:${servletApiVersion}")
    //providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')

    /*
     * Test libraries
     */
    // Enable use of the JUnitPlatform Runner within the IDE
    testCompile("org.junit.platform:junit-platform-runner:${junitPlatformVersion}")
    testCompile("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testRuntime("org.apache.logging.log4j:log4j-jul:${log4j2Version}")

    testCompile("javax.servlet:javax.servlet-api:${servletApiVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    // Needed when testing Mybatis to create a connection via JDBC
    testCompile("com.zaxxer:HikariCP:${hikariCPVersion}")
    testCompile("org.mybatis.spring.boot:mybatis-spring-boot-starter-test:${myBatisBootVersion}")
}

// Used by Flyway when Gradle is used in command line mode
flyway {
    url = datasourceUrl
    user = datasourceUsername
    password = datasourcePassword
    encoding = 'UTF-8'
}

junitPlatform {
    platformVersion junitPlatformVersion
    filters {
        engines {
            include 'junit-jupiter' // We only want to run JUnit Jupiter tests
            // include 'junit-vintage'
            // exclude 'custom-engine'
        }
        tags {
            // include 'fast'
            exclude 'slow'
        }
        // includeClassNamePattern '.*Test'
    }
    // enableStandardTestTask true
    // reportsDir file('build/test-results/junit-platform') // this is the default
    logManager 'org.apache.logging.log4j.jul.LogManager'
}

war {
    baseName = 'avocado-spring-boot'
    version = version
    archiveName = baseName + '-' + version + '.war'

    doLast {
        println "War build at: " + getTimestamp()
    }
}

/**
 * Explode the domain war when task 'war' ends.
 * Needed for local development when using Weblogic and deploying the exploded version
 */
task explodedWar(type: Copy) {
    into "$buildDir/libs/exploded/" + war.archiveName
    with war
}
war.dependsOn explodedWar

/**
 * Pre-process any needed resource file when copying it to WEB-INF/classes
 */
processResources {
    // Include Env configuration for specified profile
    from "../avocado-config/etc/env/$propertyDrivenProfile"
    exclude "**/*.gradle"
    filesMatching("**/*.properties") { fcd ->
        fcd.expand(project.properties)
    }
}

/**
 * Helper functions.
 */

/**
 * @return current timestamp
 */
static def getTimestamp() {
    def date = new Date()
    return date.format('yyyy.MM.dd HH:mm')
}

/**
 * Configure the needed properties depending on user specified profile ( using 'gradle task -PprofileName').
 *
 * @return active profile name
 */
def configForActiveProfile() {
    def activeProfile
    def base = '../avocado-config/etc/env/'
    if (project.hasProperty('prod')) {
        // Used for production environment
        activeProfile = 'prod'
    } else if (project.hasProperty('acc')) {
        // Used for acceptance environment
        activeProfile = 'acc'
    } else {
        // Default when no profile property is specified, used for testing
        activeProfile = 'local'
    }
    println "Selected profile is: '${activeProfile}'"
    apply from: project.file(base + activeProfile + '/profile_' + activeProfile + '.gradle')
    return activeProfile
}
