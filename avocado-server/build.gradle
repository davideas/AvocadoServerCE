apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

group 'eu.davidea'
version '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

ext {
    appName = 'Avocado Server Community Edition'
    builtBy = System.properties['user.name'] + "@" + getTimestamp()
}

// load config for active profile
def propertyDrivenProfile = configForActiveProfile()

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

war {
    baseName = 'avocadoce'
    version =  version
    archiveName = 'avocadoce.war'

    doLast {
        println "War build at: " + getTimestamp()
    }
}

configurations {
    providedRuntime
    compile.exclude module: 'spring-boot-starter-tomcat'
    compile.exclude group: 'org.apache.tomcat'
    compile.exclude group: 'com.zaxxer'
    compile.exclude group: 'ch.qos.logback'
}

dependencies {
    compile("javax.validation:validation-api:2.0.0.Final")
    compile('org.flywaydb:flyway-core')
    compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:${myBatisBootVersion}")
    compile('org.springframework.boot:spring-boot-starter-web') {
        //exclude module: "spring-boot-starter-tomcat"
    }

    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    compile("mysql:mysql-connector-java:6.0.6")

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-log4j2
    compile("org.springframework.boot:spring-boot-starter-log4j2:${springBootVersion}")
    // https://mvnrepository.com/artifact/com.lmax/disruptor
    compile("com.lmax:disruptor:3.3.6")

    compileOnly("javax.servlet:javax.servlet-api:3.1.0")
    //providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')

    //testCompile("javax.servlet:javax.servlet-api:3.1.0")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    // Needed when testing Mybatis
    //testCompile('commons-dbcp:commons-dbcp:1.4')
}

/**
 * Explode the domain war when task 'war' ends.
 * Needed for local development when using Weblogic and deploying the exploded version
 */
task explodedWar(type: Copy) {
    into "$buildDir/exploded"
    with war
}
war.dependsOn explodedWar


/**
 * Pre-process any needed resource file when copying it to WEB-INF/classes
 */
processResources {
    // Include Log4j configuration for specified profile
    from "etc/env/$propertyDrivenProfile"

    exclude "**/*.gradle"

    // exclude('ecas-config-*.properties')
    filesMatching("**/*.properties") { fcd ->
        if (!fcd.name.startsWith('ecas-config')) {
            fcd.expand(project.properties)
        }
    }
}

/**
 * Helper functions.
 */

/**
 * @return current timestamp
 */
static def getTimestamp() {
    def date = new Date()
    return date.format('yyyy.MM.dd HH:mm')
}

/**
 * Configure the needed properties depending on user specified profile ( using 'gradle task -PprofileName').
 *
 * @return active profile name
 */
def configForActiveProfile() {
    def activeProfile
    def base = '../avocado-config/etc/env/'
    if (project.hasProperty('prod')) {
        // Used for production environment
        activeProfile = 'prod'
        apply from: project.file(base + activeProfile + '/profile_prod.gradle');
    } else if (project.hasProperty('acc')) {
        // Used for acceptance environment
        activeProfile = 'acc'
        apply from: project.file(base + activeProfile + '/profile_acc.gradle');
    } else if (project.hasProperty('dev')) {
        // Used for test environment
        activeProfile = 'dev'
        apply from: project.file(base + activeProfile + '/profile_dev.gradle');
    } else {
        // Default when no profile property is specified, used for testing
        activeProfile = 'local'
        apply from: project.file(base + activeProfile + '/profile_local.gradle');
    }
    println ">> Selected profile is: '${activeProfile}'"
    return activeProfile
}
